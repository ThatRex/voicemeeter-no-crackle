@echo off

title Voicemeeter No Crackle

set TASK_NAME=voicemeeter-no-crackle
set SCRIPT_NAME=audiodg-fix.ps1
set SCRIPT_LOCATION=C:\%SCRIPT_NAME%
set ARGUMENTS=-ExecutionPolicy Bypass -File %SCRIPT_LOCATION%

@REM checks self running with admin privileges; relaunches self with admin privileges if not
net session >nul 2>&1 || (
    powershell -Command "Start-Process cmd -Verb RunAs -ArgumentList '/c ""%~dpnx0""'"
    exit
)

:menu

cls

echo Voicemeeter No Crackle
echo https://github.com/ThatRex/voicemeeter-no-crackle
echo.
echo Choose an option:
echo 1. Add To Startup
echo 2. Remove From Startup
echo 3. Exit
choice /c 123 /n

cls

if errorlevel 3 (

    exit

) else if errorlevel 2 (

    schtasks /delete /tn %TASK_NAME% /f
    del %SCRIPT_LOCATION%

) else if errorlevel 1 (


    if exist %SCRIPT_LOCATION% (
        del %SCRIPT_LOCATION%
    )
    echo # File Generated By Voicemeeter No Crackle >> %SCRIPT_LOCATION%
    echo # https://github.com/ThatRex/voicemeeter-no-crackle >> %SCRIPT_LOCATION%
    echo $Process = Get-Process -Name audiodg >> %SCRIPT_LOCATION%
    echo $Process.ProcessorAffinity = 1 >> %SCRIPT_LOCATION%
    echo $Process.PriorityClass = [System.Diagnostics.ProcessPriorityClass]::RealTime >> %SCRIPT_LOCATION%
    schtasks /create /tn %TASK_NAME% /sc onlogon /delay 0000:30 /tr "powershell %ARGUMENTS%" /ru system /rl highest /f

)

pause

goto :menu
